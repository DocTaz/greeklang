apply plugin: "base"
apply plugin:  "groovy"
apply plugin: 'application'
apply plugin:  "maven"

group = "edu.holycross.shot"
version = '0.0.1'

mainClassName = "Parser"

apply from: "versions.gradle"

if (hasProperty('pub')) {
    System.err.print "Using configuration data from ${pub}"
    File confFile = new File(pub)
    if (! confFile.exists()) {
        throw new Exception("No publication configuration file ${pub} found.")
    } else {
        apply from: pub
    }

} else {
    File confFile = new File("pub.gradle")
    if (! confFile.exists()) {
        throw new Exception("No publication configuration file ${confFile} found\
.")
    } else {
        println "Using default configuration in 'pub.gradle'"
        apply from: "pub.gradle"
    }
}



repositories {
    mavenCentral()
    maven {
        url "http://beta.hpcc.uh.edu/nexus/content/repositories/releases"
    }
    maven {
      url "http://beta.hpcc.uh.edu/nexus/content/repositories/papyriinfo/"
    }
}

dependencies {
  compile group: 'org.codehaus.groovy', name: 'groovy-all', version: groovyVersion
  compile group: 'org.xerial',name: 'sqlite-jdbc', version: sqlLiteVersion

  compile group: 'edu.harvard.chs', name : 'cite' , version: '0.90.0'

  compile group : 'edu.unc.epidoc', name: 'transcoder', version : epidocVersion
  compile group: 'net.sf.opencsv', name: 'opencsv', version : opencsvVersion

  testCompile group: 'junit', name: 'junit', version: junitVersion
  testCompile group :'xmlunit', name: 'xmlunit', version: xmlunitVersion
}


task inflClass(type: Copy) {
  from ("data/inflection") {
    include "*.csv"
  }
  into "${buildDir}"
}

task morphStems() {
  description = "Creates composite csv file for morphological stems collection."
}
morphStems.doLast {
  if (! buildDir.exists()) {
    buildDir.mkdir()
  }
  File stems = new File(buildDir, "morphstems.csv")  
  stems.setText("StemUrn,LexicalEntity,Lemma,Stem,StemClass,Application\n")

  def srcDirs = ["data/nounstemsrc", "data/verbstemsrc"]

  srcDirs.each {
    File dir = new File(it)
    dir.eachFileMatch(~/.*.csv/) { csvFile ->  
      Integer count = 0
      csvFile.eachLine { ln ->
	if (count > 0) {
	  stems.append("${ln}\n", "UTF-8")
	}
	count++;
      }
    }
  }
}


task endings() {
  description = "Creates composite csv file for endings collection."
}

endings.doLast {
  if (! buildDir.exists()) {
    buildDir.mkdir()
  }
  File endingsFile = new File(buildDir, "endings.csv")
  endingsFile.setText("EndingUrn,EndingString,Form,FormSet\n")

  def srcDirs = ["data/nounendsrc", "data/verbendsrc"]
  srcDirs.each {
    File dir = new File(it)
    dir.eachFileMatch(~/.*.csv/) { csvFile ->  
      Integer count = 0
      csvFile.eachLine { ln ->
	if (count > 0) {
	  endingsFile.append("${ln}\n", "UTF-8")
	}
	count++;
      }
    }
  }
}

task stemTypes() {
  description = "Creates composite csv file for stemtypes collection."
}
stemTypes.doLast {
  if (! buildDir.exists()) {
    buildDir.mkdir()
  }
  File stemtypes = new File(buildDir, "stemtypes.csv")
  stemtypes.setText("MorphStemType,Label,EndingsClass")


  def srcDirs = ["data/stemtypesrc"]

  srcDirs.each {
    File dir = new File(it)
    dir.eachFileMatch(~/.*.csv/) { csvFile ->  
      Integer count = 0
      csvFile.eachLine { ln ->
	if (count > 0) {
	  stemtypes.append("${ln}\n", "UTF-8")
	}
	count++;
      }
    }
  }
}



task tags() {
  description = "Creates composite csv file with a tag record for each ending."
}
tags.doLast {
  File tags = new File(buildDir, "tags.csv")
  tags.setText("TaggingUrn,Ending,Tag\n")
  Integer tagCount = 0


  def stdSrc = ["data/nounendsrc/decl2-standard.csv", "data/verbendsrc/standard.csv"]
  def epicSrc = ["data/nounendsrc/decl2-epic.csv"]



  stdSrc.each { 
    File src = new File(it)
    Integer lineCount = 0
    src.eachLine {  ln ->
      if (lineCount > 0) {
	def cols = ln.split(/,/)
	if ((cols.size() > 0) && (cols[0].size() > 0)) {
	  tagCount++;
	  tags.append("urn:cite:ns:morphtag.${tagCount},${cols[0]},standard\n")
	}
      }
      lineCount++;
    }
  }


  epicSrc.each { 
    File src = new File(it)
    Integer lineCount = 0
    src.eachLine {  ln ->
      if (lineCount > 0) {
	def cols = ln.split(/,/)
	if ((cols.size() > 0) && (cols[0].size() > 0)) {
	  tagCount++;
	  tags.append("urn:cite:ns:morphtag.${tagCount},${cols[0]},epic\n")
	}
      }
      lineCount++;
    }
  }  
}

task csv(type: Copy, dependsOn: [inflClass, morphStems, endings, stemTypes, tags]) {
  description = "Builds CITE Collection of 5 composite csv files and XML inventory from multiple source files."
  from ("data") {
    include "inventory.xml"
  }
  into "${buildDir}"
}

csv.doLast {
  println  "Composite csv files and CITE Collection inventory now in build area."
}

task db(dependsOn: ["csv"]) {
  description = "Builds sqlite database from csv source."
}

db.doLast {
  println "Build sqlite db now."
  // will have to use a buildSrc class to wrap MorphSql presumably?
}



uploadArchives {
    repositories.mavenDeployer {
        repository(url: nexusRepo) {                   
            authentication (userName: nexusUser, password: nexusPassword)
        }
    }
}



// add csv files to executable jar?  or sqlite binary?
task uberjar(type: Jar,dependsOn:[':compileJava',':compileGroovy']) {
    from files(sourceSets.main.output.classesDir)
    from configurations.runtime.asFileTree.files.collect { zipTree(it) }

    manifest {
        attributes 'Main-Class': mainClassName
    }
}
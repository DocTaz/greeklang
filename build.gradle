apply plugin: "base"
apply plugin:  "groovy"
apply plugin:  "java"
apply plugin:  "maven"

group = "edu.holycross.shot"
ext {
  utilsversion = '1.0.0'
}


apply from: "versions.gradle"
apply from: "conf.gradle"
apply plugin: 'edu.holycross.shot.concmd'

import com.github.rjeschke.txtmark.*
import org.apache.tools.ant.filters.*
buildscript {
  repositories {
    mavenCentral()
    maven {
  	  url "http://beta.hpcc.uh.edu/nexus/content/groups/public"
  	}
  }
  dependencies {
    classpath group: "edu.holycross.shot", name: "concmd", version: "1.0.2", classifier: "all"
  }
}

allprojects {
  task printInfo << {
    println "This is ${project.name}"
  }
}

subprojects {

  apply plugin: "groovy"
  apply plugin: "java"

  repositories {
     mavenCentral()
     maven {
       url "http://beta.hpcc.uh.edu/nexus/content/groups/public"
     }
  }
  dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: versionMap['groovy']

    compile group : 'edu.unc.epidoc', name: 'transcoder', version: versionMap['epidoc']
    compile group: 'edu.harvard.chs', name : 'cite' , version: versionMap['cite']

    testCompile group : 'edu.unc.epidoc', name: 'transcoder', version: versionMap['epidoc']
    testCompile group: 'junit', name: 'junit', version: versionMap['junit']
    testCompile group: 'org.concordion', name : 'concordion', version: versionMap['concordion']


  }

  task cpSpecs(type: Copy) {
    description "Copies all source for concordion tests to build area."
    from "specs"
    into "${buildDir}/specs"
  }
  task prepareConc(type: edu.holycross.shot.ConcMdTask, dependsOn: cpSpecs) {
      description "Converts source MD to HTML"
      from "specs/md"
      into "${buildDir}/specs/md"
  }
  test.dependsOn prepareConc
  task conc(type: Copy, dependsOn: test) {
    description "Filter-copies concordion output for final formatting"
    from "${buildDir}/concordion-unfiltered"
    into "${buildDir}/concordion-formatted"
    filter(ReplaceTokens, tokens: tokenMap)
  }
  test {
    systemProperties 'concordion.output.dir': file("${buildDir}/concordion-unfiltered")
  }

  conc.doLast {
    println "\nFormatted output is in ${buildDir}/concordion-formatted"
  }


  sourceSets {
      main {
          java {
              srcDir 'src'
          }
          resources {
              srcDir 'src'
          }
      }
      test {
          java {
              //srcDir "${project.projectDir}/specs/java"
              srcDir "specs/java"
          }
          resources {
            srcDir "${buildDir}/specs/md"
            //srcDir "specs/md"
          }
      }
  }


}

project(":morphology") {
  apply from: "${rootProject.rootDir}/morphology/fstconf.gradle"
  dependencies {
    compile project(':common')
    testCompile project(':common')
  }
}
